&НаКлиенте
Перем ПараметрИсправленияТекущейОбласти;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(ПараметрыСеанса.МОД_ПоследниеФормыМакеты) Тогда
		Сообщить(НСтр("ru = 'Не определена форма или макет для модификации'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	СистемнаяИнформация = Новый СистемнаяИнформация;
	ВерсияПлатформы = СистемнаяИнформация.ВерсияПриложения;
	Если НЕ ДоступнаВерсияПлатформы(ЭтаФорма, "8.3.22") Тогда
		Элементы.ТабличныйДокументКонтекстноеМенюУдалитьКолонкуСправа.Видимость = Ложь;
	КонецЕсли;

	ИмяМодифицируемойФормыМакета = ПараметрыСеанса.МОД_ПоследниеФормыМакеты[0];
	Заголовок = ИмяМодифицируемойФормыМакета;

	ЗагрузитьИзмененияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы = Истина Тогда
		Возврат;
	КонецЕсли;

	Если Модифицированность Тогда
		Отказ = Истина;
		ОбработчикПродолжения = Новый ОписаниеОповещения("ПередЗакрытиемПослеПодтверждения", ЭтаФорма);
		ПоказатьВопрос(ОбработчикПродолжения, НСтр("ru = 'Несохраненные изменения будут потеряны. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемПослеПодтверждения(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКолонкуСправа(Команда)
	ВыделенныеОбласти = Элементы.ТабличныйДокумент.ПолучитьВыделенныеОбласти();
	
	Если ВыделенныеОбласти.Количество() > 1 Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Можно увеличить только одну область'"));
		Возврат;
	КонецЕсли;

	Если ВыделенныеОбласти[0].Текст <> "Имя" Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Необходимо выбрать колонку Имя'"));
		Возврат;
	КонецЕсли;
	
	ДобавитьКолонкуСправаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКолонкуСправа(Команда)
	ВыделенныеОбласти = Элементы.ТабличныйДокумент.ПолучитьВыделенныеОбласти();
	
	Если ВыделенныеОбласти.Количество() > 1 Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Можно уменьшить только одну область'"));
		Возврат;
	КонецЕсли;
	
	ТекущаяОбласть = ВыделенныеОбласти[0];
	Если ТекущаяОбласть.Текст <> "Имя" Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Необходимо выбрать колонку Имя'"));
		Возврат;
	КонецЕсли;
	
	Для Инд = 1 По ТабличныйДокумент.ВысотаТаблицы Цикл
		ОбластьСтроки = ТабличныйДокумент.Область(Инд, ТекущаяОбласть.Право, Инд, ТекущаяОбласть.Право);
		Если ОбластьСтроки.ФорматСтрок = ТекущаяОбласть.ФорматСтрок
			И ЗначениеЗаполнено(ОбластьСтроки.Текст) Тогда
			
			ПоказатьПредупреждение( , НСтр("ru = 'В удаляемой области есть данные'"));
			Возврат;
		КонецЕсли;
	КонецЦикла;

	УдалитьКолонкуСправаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьJSON(Команда)
	ТекстJSON = ПолучитьJSONНаСервере();

	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстJSON);
	ТекстовыйДокумент.Показать(ИмяМодифицируемойФормыМакета);
КонецПроцедуры

&НаСервере
Функция ПолучитьJSONНаСервере()
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектОбработки.ПреобразоватьТабличныйДокументВJSON(ТабличныйДокумент);
КонецФункции

&НаСервере
Функция ПрочитатьJSONТабличногоДокумента(Текст)
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектОбработки.ПреобразоватьJSONВТабличныйДокумент(Текст);
КонецФункции

&НаКлиенте
Процедура Нормализовать(Команда)
	НормализоватьНаСервере();
КонецПроцедуры

&НаСервере
Функция НормализоватьНаСервере()
	ТекстJSON = ПолучитьJSONНаСервере();
	ТабличныйДокумент = ПрочитатьJSONТабличногоДокумента(ТекстJSON);
КонецФункции

&НаКлиенте
Процедура УстановитьJSON(Команда)
	ОбработчикПродолжения = Новый ОписаниеОповещения("УстановитьJSONПослеВводаСтроки", ЭтаФорма);
	ПоказатьВводСтроки(ОбработчикПродолжения, , НСтр("ru = 'Введите строку JSON'"), , Истина);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьJSONПослеВводаСтроки(Строка, ДополнительныеПараметры) Экспорт
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент = ПрочитатьJSONТабличногоДокумента(Строка);
	ТипДокумента = "Текстовый";
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	ЗаписатьИзмененияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияНаСервере()
	Если ТипДокумента = "Табличный" Тогда
		Документ = ТабличныйДокумент;
	ИначеЕсли ТипДокумента = "Текстовый" Тогда
		ТекстJSON = ПолучитьJSONНаСервере();

		Документ = Новый ТекстовыйДокумент;
		Документ.УстановитьТекст(ТекстJSON);
	КонецЕсли;

	АдресМакетаИзменений = ПараметрыСеанса.МОД_АдресМакетаИзменений.Получить(ИмяМодифицируемойФормыМакета);
	Если АдресМакетаИзменений = Неопределено Тогда
		Соответствие = Новый Соответствие(ПараметрыСеанса.МОД_АдресМакетаИзменений);
		Соответствие.Вставить(ИмяМодифицируемойФормыМакета, ПоместитьВоВременноеХранилище(Документ,
			Новый УникальныйИдентификатор));

		ПараметрыСеанса.МОД_АдресМакетаИзменений = Новый ФиксированноеСоответствие(Соответствие);
	Иначе
		ПоместитьВоВременноеХранилище(Документ, АдресМакетаИзменений);
	КонецЕсли;

	Модифицированность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФормуМакет(Команда)
	ОбработчикПродолжения = Новый ОписаниеОповещения("ВыбратьФормуМакетПослеПодтверждения", ЭтаФорма);
	Если Модифицированность Тогда
		ПоказатьВопрос(ОбработчикПродолжения, НСтр("ru = 'Несохраненные изменения будут потеряны. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОбработчикПродолжения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФормуМакетПослеПодтверждения(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	ОбработчикПродолжения = Новый ОписаниеОповещения("ВыбратьФормуМакетПослеВыбора", ЭтаФорма);
	ПараметрыФормы = Новый Структура("Заголовок,СписокВыбора"
		,НСтр("ru = 'Последние формы / макеты'")
		,ПолучитьСписокФормМакетов());
	ОткрытьФорму("Обработка.МОД_МодификацияУправляемойФормы.Форма.ВыборИзСписка", ПараметрыФормы, , , , , ОбработчикПродолжения);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФормуМакетПослеВыбора(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ИмяМодифицируемойФормыМакета = ВыбранныйЭлемент;
		Заголовок = ИмяМодифицируемойФормыМакета;
		
		ЗагрузитьИзмененияФормы();
		Модифицированность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзмененияФормы()
	Если СтрНачинаетсяС(ИмяМодифицируемойФормыМакета, "ОбщийМакет")
		ИЛИ Найти(ИмяМодифицируемойФормыМакета, ".Макет.") > 0 Тогда
		
		СтруктураМакет = Новый Структура("ИмяМакета", ИмяМодифицируемойФормыМакета);
		МакетИзменений = МОД_МодификацияУправляемыхФорм.ПолучитьМакет(СтруктураМакет);
	Иначе
		СтруктураФорма = Новый Структура("ИмяФормы", ИмяМодифицируемойФормыМакета);
		МакетИзменений = МОД_МодификацияУправляемыхФорм.ПолучитьМакетИзменений(СтруктураФорма);
	КонецЕсли;
	
	Если ТипЗнч(МакетИзменений) = Тип("ТабличныйДокумент") Тогда
		ТабличныйДокумент = МакетИзменений;
		ТипДокумента = "Табличный";
	ИначеЕсли ТипЗнч(МакетИзменений) = Тип("ТекстовыйДокумент") Тогда
		ТабличныйДокумент = ПрочитатьJSONТабличногоДокумента(МакетИзменений.ПолучитьТекст());
		ТипДокумента = "Текстовый";
	Иначе
		// по умолчанию
		ТипДокумента = "Текстовый";
	КонецЕсли;
	
	ИмяТекущейОбласти = "";
	ПредыдущаяСтрока = 0;
	ОбновитьСписокОбластей();
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокФормМакетов()
	СписокРезультат = Новый СписокЗначений;
	Для Каждого ФормаМакет Из ПараметрыСеанса.МОД_ПоследниеФормыМакеты Цикл
		ЭлементСписка = СписокРезультат.Добавить(ФормаМакет);
		Если ПараметрыСеанса.МОД_АдресМакетаИзменений.Получить(ЭлементСписка.Значение) <> Неопределено Тогда
			ЭлементСписка.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат СписокРезультат;
КонецФункции

&НаКлиенте
Процедура УдалитьИзменения(Команда)
	ОбработчикПродолжения = Новый ОписаниеОповещения("УдалитьИзмененияПослеПодтверждения", ЭтаФорма);
	ПоказатьВопрос(ОбработчикПродолжения, НСтр("ru = 'Вы действительно хотите удалить изменения?'"),
		РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзмененияПослеПодтверждения(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	УдалитьИзмененияНаСервере();
КонецПроцедуры

&НаСервере
Процедура УдалитьИзмененияНаСервере()
	Соответствие = Новый Соответствие(ПараметрыСеанса.МОД_АдресМакетаИзменений);
	Соответствие.Удалить(ИмяМодифицируемойФормыМакета);

	ПараметрыСеанса.МОД_АдресМакетаИзменений = Новый ФиксированноеСоответствие(Соответствие);
КонецПроцедуры

&НаСервере
Процедура ДобавитьКолонкуСправаНаСервере()
	ТекущаяОбласть = Элементы.ТабличныйДокумент.ПолучитьВыделенныеОбласти()[0];
	
	НоваяОбласть = ТабличныйДокумент.Область( , ТабличныйДокумент.ШиринаТаблицы + 1, ТекущаяОбласть.Верх, ТабличныйДокумент.ШиринаТаблицы + 1);
	ОбластьВставки = ТабличныйДокумент.Область( , ТекущаяОбласть.Право + 1, ТекущаяОбласть.Верх, ТекущаяОбласть.Право + 1);
	ТабличныйДокумент.ВставитьОбласть(НоваяОбласть, ОбластьВставки, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ТекущаяОбласть.Лево, ТекущаяОбласть.Низ, ТекущаяОбласть.Право + 1).Объединить();
КонецПроцедуры

&НаСервере
Процедура УдалитьКолонкуСправаНаСервере()
	ТекущаяОбласть = Элементы.ТабличныйДокумент.ПолучитьВыделенныеОбласти()[0];
	
	УдаляемаяОбласть = ТабличныйДокумент.Область( , ТекущаяОбласть.Право, ТекущаяОбласть.Верх, ТекущаяОбласть.Право);
	ТабличныйДокумент.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДоступнаВерсияПлатформы(Форма, СтрокаВерсии) Экспорт
	МассивВерсияПлатформы = СтрРазделить(Форма.ВерсияПлатформы, ".");
	МассивВерсия = СтрРазделить(СтрокаВерсии, ".");
	
	Для Инд = 1 По МассивВерсия.Количество() Цикл
		Если Число(МассивВерсия[Инд - 1]) > Число(МассивВерсияПлатформы[Инд - 1]) Тогда
			Возврат Ложь;
		ИначеЕсли Число(МассивВерсия[Инд - 1]) < Число(МассивВерсияПлатформы[Инд - 1]) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

&НаСервере
Процедура ОбновитьСписокОбластей()
	ТаблицаОбласти = Новый ТаблицаЗначений;
	ТаблицаОбласти.Колонки.Добавить("Имя");
	ТаблицаОбласти.Колонки.Добавить("Верх");
	
	Для Каждого Область Из ТабличныйДокумент.Области Цикл
		НоваяСтрока = ТаблицаОбласти.Добавить();
		НоваяСтрока.Имя = Область.Имя;
		НоваяСтрока.Верх = Область.Верх;
	КонецЦикла;
	
	ТаблицаОбласти.Сортировать("Верх");
	Элементы.ИмяТекущейОбласти.СписокВыбора.Очистить();
	Элементы.ИмяТекущейОбласти.СписокВыбора.Добавить("", НСтр("ru = '<все>'"));

	Для Каждого СтрокаТаблицы Из ТаблицаОбласти Цикл
		Элементы.ИмяТекущейОбласти.СписокВыбора.Добавить(СтрокаТаблицы.Имя);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ИмяТекущейОбластиПриИзменении(Элемент)
	ИмяТекущейОбластиПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИмяТекущейОбластиПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(ИмяТекущейОбласти) Тогда
		ТекущаяОбласть = ТабличныйДокумент.Области[ИмяТекущейОбласти];
		Если ТекущаяОбласть.Верх > 1 Тогда
			ТабличныйДокумент.Область(1, , ТекущаяОбласть.Верх - 1).Видимость = Ложь;
		КонецЕсли;

		ТабличныйДокумент.Область(ТекущаяОбласть.Верх, , ТекущаяОбласть.Низ).Видимость = Истина;
		
		Если ТекущаяОбласть.Низ < ТабличныйДокумент.ВысотаТаблицы Тогда
			ТабличныйДокумент.Область(ТекущаяОбласть.Низ + 1, , ТабличныйДокумент.ВысотаТаблицы).Видимость = Ложь;
		КонецЕсли;
		
		ШиринаИмени = 0;
		Пока ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ШиринаИмени + 1).Текст = "Имя" Цикл
			ШиринаИмени = ШиринаИмени + 1;
		КонецЦикла;
		
		ТабличныйДокумент.ФиксацияСверху = ТекущаяОбласть.Верх;
		ТабличныйДокумент.ФиксацияСлева = ШиринаИмени;
	Иначе
		ТабличныйДокумент.Область(1, , ТабличныйДокумент.ВысотаТаблицы).Видимость = Истина;
		ТабличныйДокумент.ФиксацияСверху = 0;
		ТабличныйДокумент.ФиксацияСлева = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТолькоУстановленные(Команда)
	Элементы.ТолькоУстановленные.Пометка = НЕ Элементы.ТолькоУстановленные.Пометка;
	Если Элементы.ТолькоУстановленные.Пометка Тогда
		ПредыдущаяСтрока = 0;
	КонецЕсли;
	
	ОбновитьВидимостьКолонок(ТабличныйДокумент.ТекущаяОбласть.Верх, ТабличныйДокумент.ТекущаяОбласть.Лево);
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументПриАктивизации(Элемент)
	Если Элементы.ТолькоУстановленные.Пометка Тогда
		ТекущаяКолонка = ТабличныйДокумент.ТекущаяОбласть.Лево;
		Если ПредыдущаяСтрока <> ТабличныйДокумент.ТекущаяОбласть.Верх Тогда
			ПараметрИсправленияТекущейОбласти = ТекущаяКолонка;
			
			ОбновитьВидимостьКолонок(ТабличныйДокумент.ТекущаяОбласть.Верх, ТекущаяКолонка);
			ПредыдущаяСтрока = ТабличныйДокумент.ТекущаяОбласть.Верх;
			ПодключитьОбработчикОжидания("ИсправлениеТекущейОбласти", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИсправлениеТекущейОбласти()
	Если ТабличныйДокумент.ТекущаяОбласть.Лево <> ПараметрИсправленияТекущейОбласти
		ИЛИ ТабличныйДокумент.ТекущаяОбласть.Право <> ПараметрИсправленияТекущейОбласти Тогда
	
		ТекущаяОбласть = ТабличныйДокумент.Область(ТабличныйДокумент.ТекущаяОбласть.Верх, ПараметрИсправленияТекущейОбласти
			,ТабличныйДокумент.ТекущаяОбласть.Низ, ПараметрИсправленияТекущейОбласти);
		
		Если ТекущаяОбласть.Видимость Тогда
			МассивОбласти = Новый Массив;
			МассивОбласти.Добавить(ТекущаяОбласть);
			Элементы.ТабличныйДокумент.УстановитьВыделенныеОбласти(МассивОбласти);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьКолонок(ТекущаяСтрока, ТекущаяКолонка)
	Если ПредыдущаяСтрока <> 0 Тогда
		Для Инд = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
			ТабличныйДокумент.Область( , Инд, ПредыдущаяСтрока, Инд).Видимость = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Если Элементы.ТолькоУстановленные.Пометка Тогда
		ИменованнаяОбласть = ИменованнаяОбласть(ТабличныйДокумент, ТекущаяСтрока);
		
		Если ИменованнаяОбласть <> Неопределено
			И ТекущаяСтрока <> ИменованнаяОбласть.Верх Тогда
			
			ШиринаИмени = 0;
			Пока ТабличныйДокумент.Область(ИменованнаяОбласть.Верх, ШиринаИмени + 1).Текст = "Имя" Цикл
				ШиринаИмени = ШиринаИмени + 1;
			КонецЦикла;
			
			Для Инд = ШиринаИмени + 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
				ТабличныйДокумент.Область( , Инд, ТекущаяСтрока, Инд).Видимость = (Инд = ТекущаяКолонка
					ИЛИ ЗначениеЗаполнено(ТабличныйДокумент.Область(ТекущаяСтрока, Инд).Текст));
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИменованнаяОбласть(ТабличныйДокумент, ТекущаяСтрока)
	ВерхОбласти = Неопределено;
	ИменованнаяОбласть = Неопределено;
	Для Каждого Область Из ТабличныйДокумент.Области Цикл
		Если ТекущаяСтрока >= Область.Верх
			И (ВерхОбласти = Неопределено
				ИЛИ Область.Верх < ВерхОбласти) Тогда
		
			ВерхОбласти = Область.Верх;
			ИменованнаяОбласть = Область;
		КонецЕсли;
	КонецЦикла;
	
	Если ИменованнаяОбласть <> Неопределено Тогда
		Возврат ИменованнаяОбласть;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ВыбратьЗначениеЯчейки(Команда)
	Если ТабличныйДокумент.ТекущаяОбласть.Низ - ТабличныйДокумент.ТекущаяОбласть.Верх > 1
		ИЛИ ТабличныйДокумент.ТекущаяОбласть.Право - ТабличныйДокумент.ТекущаяОбласть.Лево > 1 Тогда
	
		ПоказатьПредупреждение( , НСтр("ru = 'Должна быть выбрана единственная ячейка'"));
		Возврат;
	КонецЕсли;
	
	ИменованнаяОбласть = ИменованнаяОбласть(ТабличныйДокумент, ТабличныйДокумент.ТекущаяОбласть.Верх);
	Если ИменованнаяОбласть = Неопределено Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Не удалось определить именованную область'"));
		Возврат;
	КонецЕсли;
	
	ЧастиИмени = СтрРазделить(ИменованнаяОбласть.Имя, "_");
	ИмяКолонки = ТабличныйДокумент.Область(ИменованнаяОбласть.Верх, ТабличныйДокумент.ТекущаяОбласть.Лево).Текст;
	
	Если ЧастиИмени[0] = "Элементы"
		И ИмяКолонки = "Тип" Тогда
			
		МассивЗначения = СтрРазделить("ГруппаФормы,ДекорацияФормы,КнопкаФормы,ПолеФормы", ",");
	Иначе
		Поз = Найти(ИмяКолонки, "(");
		Если Поз = 0 Тогда
			ПоказатьПредупреждение( , НСтр("ru = 'Не удалось определить имя конструктора'"));
			Возврат;
		Иначе
			ИмяКонструктора = Лев(ИмяКолонки, Поз - 1);
		КонецЕсли;
		
		МассивЗначения = ПолучитьИменаЗначенийПеречисления(ИмяКонструктора);
		Если НЕ ЗначениеЗаполнено(МассивЗначения) Тогда
			ПоказатьПредупреждение( , НСтр("ru = 'Не удалось получить список значений для конструктора. Возможно имя указано неверно'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СписокЗначения = Новый СписокЗначений;
	СписокЗначения.ЗагрузитьЗначения(МассивЗначения);

	ОбработчикПродолжения = Новый ОписаниеОповещения("ВыбратьЗначениеЯчейкиПослеВыбора", ЭтаФорма);
	ТекущееЗначение = СписокЗначения.НайтиПоЗначению(ТабличныйДокумент.ТекущаяОбласть.Текст);
	СписокЗначения.ПоказатьВыборЭлемента(ОбработчикПродолжения, , ТекущееЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗначениеЯчейкиПослеВыбора(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент.ТекущаяОбласть.Текст = ВыбранныйЭлемент;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИменаЗначенийПеречисления(ИмяПеречисления)
	МассивРезультат = Новый Массив;
	Попытка
		Выполнить("
		|Для Каждого Значение Из " + ИмяПеречисления + " Цикл
		|	ИмяЗначения = СтрЗаменить("","" + ПолучитьПолноеИмяПредопределенногоЗначения(Значение), "","" + ИмяПеречисления + ""."", """");
		|	МассивРезультат.Добавить(ИмяЗначения);
		|КонецЦикла;
		|");
	Исключение
	КонецПопытки;
	
	Возврат МассивРезультат;
КонецФункции
